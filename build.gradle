/*
 * Copyright 2018 Andres Almiray
 *
 * This file is part of Java Trove Examples
 *
 * Java Trove Examples is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * Java Trove Examples is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with Java Trove Examples. If not, see <http://www.gnu.org/licenses/>.
 */

plugins {
    id 'application'
    id 'com.github.robfletcher.compass' version '2.0.6'
    id 'com.github.ben-manes.versions'  version '0.20.0'
    id 'com.github.hierynomus.license'  version '0.14.0'
}

mainClassName = 'org.kordamp.javafx.sass.Demo'

repositories {
    jcenter()
}

dependencies {
    compile 'org.kordamp.desktoppanefx:desktoppanefx-core:0.8.0'
}

compass {
    cssDir = file("src/main/resources")
}

compileJava.finalizedBy('compassCompile')

dependencyUpdates.resolutionStrategy = {
    componentSelection { rules ->
        rules.all { selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'pr', 'atlassian'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

license {
    header = rootProject.file('gradle/HEADER')
    strictCheck = true
    ignoreFailures = true
    mapping {
        scss = 'SLASHSTAR_STYLE'
        java = 'SLASHSTAR_STYLE'
        fxml = 'XML_STYLE'
    }
    ext.year    = '2018'
    ext.author  = 'Andres Almiray'
    ext.project = 'Java Trove Examples'
}
